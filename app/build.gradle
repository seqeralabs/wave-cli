plugins {
    id 'groovy'
    id 'io.seqera.wave.cli.java-application-conventions'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    maven { url = "https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases" }
    maven { url = "https://s3-eu-west-1.amazonaws.com/maven.seqera.io/snapshots" }
}

dependencies {
    implementation 'io.seqera:wave-api:1.31.0'
    implementation 'io.seqera:wave-utils:1.31.0'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'com.squareup.moshi:moshi:1.15.2'
    implementation 'com.squareup.moshi:moshi-adapters:1.15.2'
    implementation 'dev.failsafe:failsafe:3.1.0'
    implementation 'org.apache.commons:commons-lang3:3.19.0'
    implementation 'org.yaml:snakeyaml:2.1'
    implementation 'org.semver4j:semver4j:5.4.0'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
    // bump commons-io version to address security vulnerabilities
    runtimeOnly 'commons-io:commons-io:2.18.0'

    testImplementation "org.codehaus.groovy:groovy:3.0.21"
    testImplementation "org.codehaus.groovy:groovy-nio:3.0.21"
    testImplementation ("org.codehaus.groovy:groovy-test:3.0.21")
    testImplementation ("org.codehaus.groovy:groovy-json:3.0.21")
    testImplementation ("cglib:cglib-nodep:3.3.0")
    testImplementation ("org.objenesis:objenesis:3.2")
    testImplementation ("org.spockframework:spock-core:2.3-groovy-3.0") { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
    testImplementation ('org.spockframework:spock-junit4:2.3-groovy-3.0') { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
}

test {
    useJUnitPlatform()
}

/*
 * Copyright 2023-2025, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

// read the version from the `VERSION` file
version = new File(rootDir,'VERSION').text.trim()
group = 'io.seqera'

application {
    // Define the main class for the application.
    mainClass = 'io.seqera.wave.cli.App'
    //  Run the Graalvm agent to resolve dynamic proxies configuration
    applicationDefaultJvmArgs = ["-agentlib:native-image-agent=config-merge-dir=conf/"]
}

shadowJar {
    archiveBaseName.set('wave')
    archiveClassifier.set('')
    archiveVersion.set('')
}

run {
    if( environment['JVM_OPTS'] ) {
        jvmArgs(environment['JVM_OPTS'])
    }
}

graalvmNative {
    binaries {
        main {
            imageName = 'wave'
            mainClass = 'io.seqera.wave.cli.App'
            configurationFileDirectories.from(file('conf'))

            if (System.env.getOrDefault("PLATFORM", "") == "linux-x86_64") {
                buildArgs(['--static', '--libc=musl', '--gc=G1', '-march=compatibility'])
            }

            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(21)
                vendor = JvmVendorSpec.matching("Oracle Corporation")
            }
            buildArgs.add('--enable-url-protocols=https')
            buildArgs.add('-R:MaxHeapSize=100M')
            buildArgs.add('-R:MinHeapSize=10M')
            buildArgs.add('-R:MaxNewSize=25M')
            // Initialize logback classes at build time since they're used by static logger fields
            buildArgs.add('--initialize-at-build-time=ch.qos.logback,org.slf4j')
        }
    }
    toolchainDetection = true
    testSupport = false
}

task buildInfo {
    doLast {
        def version = rootProject.file('VERSION').text.trim()
        def commitId = System.env.getOrDefault("GITHUB_SHA", "unknown").substring(0,7)
        def info = """\
                    name=${rootProject.name}
                    version=${version}
                    commitId=${commitId}
                """.stripIndent().toString()
        def f = file("src/main/resources/META-INF/build-info.properties")
        f.parentFile.mkdirs()
        f.text = info
    }
}

compileJava {
    dependsOn buildInfo
}
